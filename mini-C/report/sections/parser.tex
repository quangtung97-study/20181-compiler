\documentclass[../report.tex]{subfiles}

\begin{document}
\subsection{Quy ước đặt tên}
Các kí tự không kết thúc có thể có những hậu tố có dạng:
\begin{itemize}
\item \textbf{*\_seq}: Ví dụ: \textbf{declaration\_seq} là đại diện 
cho một dãy 
các \textbf{declaration} mà không có dấu ngăn cách giữa chúng. 
Hay: \\
\tab \textbf{declaration\_seq $\rightarrow$ declaration | 
    declaration\_seq declaration}

\item \textbf{*\_list}: Ví dụ: \textbf{param\_list} là đại diện cho 
một dãy 
các \textbf{param} mà giữa chúng có dấu ngăn cách, thông thường là 
dấu phẩy. Hay: \\
\tab \textbf{param\_list $\rightarrow$ param | param\_list , param}

\item \textbf{*\_opt}:  Ví dụ: \textbf{pointer\_opt} là đại diện 
cho một kí tự \textbf{pointer} hoặc không có kí tự nào cả. Hay: \\
\tab \textbf{pointer\_opt $\rightarrow$ pointer | $\epsilon$}
\end{itemize}

\subsection{Các sản xuất của ngôn ngữ Mini-C}
Các sản xuất được viết bằng ngôn ngữ định nghĩa của Bison, bao gồm:
\subsubsection{translation\_unit}
\begin{lstlisting}
translation_unit
    : external_declaration
    | translation_unit external_declaration
    ;
\end{lstlisting}
Là kí tự bắt đầu của văn phạm. Bao gồm một dãy các 
\textbf{external\_declaration}. \\

\subsubsection{external\_declaration}
\begin{lstlisting}
external_declaration
    : init_declaration
    | function_declaration
    | function_definition
    ;
\end{lstlisting}
Là các khai báo có thể được viết bên ngoài hàm. Bao gồm 
khai báo thông thường \textbf{init\_declaration}, khai báo 
hàm hoặc định nghĩa hàm. \\

\subsubsection{function\_declaration và function\_definition}
Khai báo và định nghĩa hàm có cấu trúc như sau: 
\begin{lstlisting}
function_declaration 
    : declaration_specifier function_declarator 
        '(' parameter_list_opt ')' ';' ;

function_definition
    : declaration_specifier function_declarator 
        '(' parameter_list_opt ')' 
        compound_statement ;
\end{lstlisting}

\subsubsection{parameter\_list}
Danh sách các tham số của hàm: 
\begin{lstlisting}
parameter_list_opt
    : %empty
    | parameter_list
    ;

parameter_list
    : declaration_specifier declarator 
    | parameter_list ',' declaration_specifier declarator
    ;
\end{lstlisting}
Trong đó \textbf{declaration\_specifier} là chỉ định 
phần tiền tố của kiểu (không bao gồm con trỏ và mảng), 
\textbf{declarator} sẽ bao gồm khai báo con trỏ, 
mảng và một định danh. Ngôn ngữ Mini-C được định nghĩa ở đây không 
hỗ trợ khai báo hay định nghĩa hàm mà 
tham số có kiểu nhưng không có tên.
số. Ví dụ: \textbf{void func(int **); }. \\

\subsection{declaration\_specifier}
Phần chỉ định khai báo, có cấu trúc như sau: 
\begin{lstlisting}
const_opt
    : %empty
    | CONST
    ;

declaration_specifier: const_opt type_specifier ;

type_specifier
    : VOID
    | UNSIGNED type_integer
    | type_integer
    | FLOAT 
    | DOUBLE
    | struct_or_union_specifier
    | enum_specifier
    ;

type_integer
    : CHAR
    | SHORT
    | INT
    | LONG
    ;
\end{lstlisting}
Trong đó \textbf{CONST} là từ khóa \textbf{const}, được sử dụng 
cho các biến hằng. Các kiểu cho phép bao gồm: 
\begin{itemize}
\item void
\item Số phẩy động: float, double
\item Số nguyên có dấu hoặc không dấu: 
    có 4 loại ứng với 4 kích thước
    khác nhau: char (1 byte), short (2 byte), 
    int (4 byte), long (8 byte). 
\item struct hoặc union
\item enum 
\end{itemize}

\subsubsection{init\_declaration}
Khai báo thông thường (có thể trong hàm hoặc ngoài hàm). 
Ở đây cho phép khai báo mà không có tên của biến 
mục đích cho các định nghĩa struct, enum hoặc union có thể 
không có tên biến sau định nghĩa. 
\begin{lstlisting}
init_declaration
    : declaration_specifier init_declarator_list ';'
    | declaration_specifier ';'
    ;

init_declarator_list 
    : init_declarator
    | init_declarator_list ',' init_declarator 
    ;

init_declarator 
    : declarator 
    | declarator '=' initializer
    ;
\end{lstlisting}

\subsubsection{declarator}
Ví dụ trong khai báo: 
\begin{lstlisting}
const int * const p, **p[100];
\end{lstlisting}
Thì \textbf{const int} là \textbf{declaration\_specifier}. \\ 
Còn \textbf{* const p}, 
\textbf{**p[100]} là các \textbf{declarator}. \\
Cũng cho phép khai báo mảng mà không có số lượng phần tử. 

\begin{lstlisting}
declarator
    : direct_declarator 
    | pointer_seq direct_declarator
    ;

pointer: '*' const_opt

pointer_seq
    : pointer
    | pointer_seq pointer
    ;

assignment_expression_opt
    : %empty
    | assignment_expression
    ;

direct_declarator
    : IDENT
    | direct_declarator '[' assignment_expression_opt ']'
    ;
\end{lstlisting}


\subsubsection{initializer}
\begin{lstlisting}
initializer
    : assignment_expression
    | '{' initializer_list '}'
    | '{' initializer_list ',' '}'
    ;

initializer_list
    : initializer
    | initializer_list ',' initializer
    ;
\end{lstlisting}

\subsubsection{struct\_or\_union\_specifier}
Định nghĩa hoặc khai báo struct và union:
\begin{lstlisting}
ident_opt
    : %empty
    | IDENT
    ;

struct_or_union_specifier
    : struct_or_union ident_opt '{' struct_declaration_seq '}'
    | struct_or_union ident_opt '{' '}'
    | struct_or_union IDENT

struct_or_union
    : STRUCT
    | UNION
    ;

struct_declaration_seq
    : struct_declaration
    | struct_declaration_seq struct_declaration
    ;

struct_declaration
    : declaration_specifier struct_declarator_list ';' 
    ;

struct_declarator_list
    : declarator
    | struct_declarator_list ',' declarator
    ;
\end{lstlisting}

\subsubsection{enum\_specifier}
Định nghĩa hoặc khai báo enum:
\begin{lstlisting}
enum_specifier
    : ENUM ident_opt '{' enumerator_list '}'
    | ENUM ident_opt '{' enumerator_list ',' '}'
    | ENUM IDENT
    ;

enumerator_list
    : enumerator
    | enumerator_list ',' enumerator
    ;

enumerator
    : IDENT 
    | IDENT '=' constant_expression
    ;
\end{lstlisting}
Trong đó có hỗ trợ dấu phẩy sau danh sách các \textbf{enumerator} 
(hay trailing comma). \\

\subsection{expression}
Các biểu thức: 
\begin{lstlisting}
primary_expression
    : IDENT
    | CONSTANT
    | STRING_LITERAL
    | '(' expression ')'
    ;

postfix_expression
    : primary_expression
    | postfix_expression '[' expression ']'
    | postfix_expression '(' argument_expression_list ')'
    | postfix_expression '(' ')'
    | postfix_expression '.' IDENT
    | postfix_expression ARROW IDENT
    | postfix_expression INCREASE
    | postfix_expression DECREASE
    ;

argument_expression_list
    : assignment_expression
    | argument_expression_list ',' assignment_expression
    ;

unary_expression
    : postfix_expression
    | INCREASE unary_expression
    | DECREASE unary_expression
    | unary_operator cast_expression
    | SIZEOF unary_expression
    /* | SIZEOF '(' type_name ')' */
    ;

unary_operator
    : '&'
    | '*'
    | '+'
    | '-'
    | '~'
    | '!'
    ;

cast_expression
    : unary_expression
    ;

multiplicative_expression
    : cast_expression
    | multiplicative_expression '*' cast_expression
    | multiplicative_expression '/' cast_expression
    | multiplicative_expression '%' cast_expression
    ;

additive_expression
    : multiplicative_expression
    | additive_expression '+' multiplicative_expression
    | additive_expression '-' multiplicative_expression
    ;

shift_expression
    : additive_expression
    | shift_expression SHIFT_LEFT additive_expression
    | shift_expression SHIFT_RIGHT additive_expression
    ;

relational_expression
    : shift_expression
    | relational_expression LT shift_expression
    | relational_expression GT shift_expression
    | relational_expression LE shift_expression
    | relational_expression GE shift_expression
    ;

equality_expression
    : relational_expression
    | equality_expression EQ relational_expression
    | equality_expression NE relational_expression
    ;

and_expression
    : equality_expression
    | and_expression '&' equality_expression
    ;

xor_expression
    : and_expression
    | xor_expression '^' and_expression
    ;

or_expression
    : xor_expression
    | or_expression '|' xor_expression
    ;

logical_and_expression
    : or_expression
    | logical_and_expression AND or_expression
    ;

logical_or_expression
    : logical_and_expression
    | logical_or_expression OR logical_and_expression
    ;

conditional_expression
    : logical_or_expression
    | logical_or_expression '?' expression ':' conditional_expression
    ;

assignment_expression
    : conditional_expression
    | unary_expression assignment_operator assignment_expression
    ;

assignment_operator
    : '='
    | ADD_ASSIGN
    | SUB_ASSIGN
    | MUL_ASSIGN
    | DIV_ASSIGN
    | REM_ASSIGN
    | AND_ASSIGN
    | OR_ASSIGN
    | XOR_ASSIGN
    | SL_ASSIGN
    | SR_ASSIGN
    ;

expression
    : assignment_expression 
    | expression ',' assignment_expression
    ;

constant_expression: conditional_expression ;
\end{lstlisting}

\subsubsection{statement}
Các loại statement:
\begin{lstlisting}
statement
    : labeled_statement
    | compound_statement
    | expression_statement
    | selection_statement
    | iteration_statement
    | jmp_statement
    ;

labeled_statement
    : IDENT ':' statement
    | CASE constant_expression ':' statement
    | DEFAULT ':' statement
    ;

compound_statement: '{' block_item_seq_opt '}' ;

block_item_seq_opt
    : %empty 
    | block_item_seq
    ;

block_item_seq
    : block_item
    | block_item_seq block_item
    ;

block_item
    : init_declaration
    | statement
    ;

expression_statement
    : ';'
    | expression ';'
    ;

selection_statement
    : IF '(' expression ')' statement 
    | IF '(' expression ')' statement ELSE statement
    | SWITCH '(' expression ')' statement
    ;

expression_opt
    : %empty
    | expression
    ;

iteration_statement
    : WHILE '(' expression ')' statement
    | DO statement WHILE '(' expression ')' ';'
    | FOR '(' expression_opt ';' expression_opt ';' expression_opt ')' statement
    | FOR '(' init_declaration expression_opt ';' expression_opt ')' statement
    ;

jmp_statement
    : GOTO IDENT ';'
    | CONTINUE ';'
    | BREAK ';'
    | RETURN expression_opt ';'
    ;
\end{lstlisting}
Trong đó có đầy đủ các lệnh trong C99.

\end{document}
